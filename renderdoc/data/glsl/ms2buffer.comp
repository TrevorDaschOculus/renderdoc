/******************************************************************************
 * The MIT License (MIT)
 *
 * Copyright (c) 2020-2022 Baldur Karlsson
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 ******************************************************************************/

#if defined(OPENGL_CORE)
#extension GL_ARB_compute_shader : require

// safe to assume this extension in compute shaders as it pre-dates compute shaders
#extension GL_ARB_shading_language_420pack : require
#endif

#include "glsl_globals.h"

layout(local_size_x = 1, local_size_y = 1, local_size_z = 1) in;

layout(binding = 0) uniform PRECISION usampler2DMSArray srcMS;
// binding = 1 used as stencil read in the depth-stencil copy fragment shaders

layout(binding = 2, std430) writeonly buffer dstBuf
{
  uint result[];
};

#ifdef VULKAN

layout(push_constant) uniform multisamplePush
{
  int numMultiSamples;
  int baseSlice;
  int baseSample;
  int byteSize;
}
mscopy;

#define numMultiSamples (mscopy.numMultiSamples)
#define baseSlice (mscopy.baseSlice)
#define baseSample (mscopy.baseSample)
#define byteSize (mscopy.byteSize)

#else

uniform ivec4 mscopy;

#define numMultiSamples (mscopy.x)
#define baseSlice (mscopy.y)
#define baseSample (mscopy.z)
#define byteSize (mscopy.w)

#endif

void main()
{
  ivec3 id = ivec3(gl_GlobalInvocationID);
  int slice = baseSlice + int(id.z / numMultiSamples);
  int sampleIdx = baseSample + int(id.z % numMultiSamples);
  uint idx = id.x + gl_NumWorkGroups.x * (id.y + (gl_NumWorkGroups.y * id.z));

  // for byteSize < 4, we sample multiple pixels to fill a single output buffer element
  if(byteSize == 1)
  {
    int w = int(textureSize(srcMS).x);
    int pxIdx = int(idx * 4);
    int x0 = (pxIdx + 0) % w;
    int y0 = (pxIdx + 0) / w;
    int x1 = (pxIdx + 1) % w;
    int y1 = (pxIdx + 1) / w;
    int x2 = (pxIdx + 2) % w;
    int y2 = (pxIdx + 2) / w;
    int x3 = (pxIdx + 3) % w;
    int y3 = (pxIdx + 3) / w;

    uvec4 data = uvec4(texelFetch(srcMS, ivec3(x0, y0, slice), sampleIdx).x,
                       texelFetch(srcMS, ivec3(x1, y1, slice), sampleIdx).x,
                       texelFetch(srcMS, ivec3(x2, y2, slice), sampleIdx).x,
                       texelFetch(srcMS, ivec3(x3, y3, slice), sampleIdx).x);
    result[idx] = (data.x << 0 | data.y << 8 | data.z << 16 | data.w << 24);
  }
  else if(byteSize == 2)
  {
    int w = int(textureSize(srcMS).x);
    int pxIdx = int(idx * 2);
    int x0 = (pxIdx + 0) % w;
    int y0 = (pxIdx + 0) / w;
    int x1 = (pxIdx + 1) % w;
    int y1 = (pxIdx + 1) / w;

    uvec2 data = uvec2(texelFetch(srcMS, ivec3(x0, y0, slice), sampleIdx).x,
                       texelFetch(srcMS, ivec3(x1, y1, slice), sampleIdx).x);
    result[idx] = (data.x << 0 | data.y << 16);
  }
  else if(byteSize == 4)
  {
    uint data = texelFetch(srcMS, ivec3(int(id.x), int(id.y), slice), sampleIdx).x;
    result[idx] = data;
  }
  else if(byteSize == 8)
  {
    uvec2 data = texelFetch(srcMS, ivec3(int(id.x), int(id.y), slice), sampleIdx).xy;
    result[idx * 2] = data.x;
    result[idx * 2 + 1] = data.y;
  }
  else if(byteSize == 16)
  {
    uvec4 data = texelFetch(srcMS, ivec3(int(id.x), int(id.y), slice), sampleIdx);
    result[idx * 4] = data.x;
    result[idx * 4 + 1] = data.y;
    result[idx * 4 + 2] = data.z;
    result[idx * 4 + 3] = data.w;
  }
}
